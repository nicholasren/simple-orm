allprojects {
  apply plugin: 'idea'
  apply plugin: 'maven'
  apply plugin: 'java'

  repositories {
    mavenCentral()
    flatDir {
      name "fileRepo"
      dirs "/tmp/repo"
    }
  }

  uploadArchives {
    repositories {
      add project.repositories.fileRepo
    }
  }

  project.ext {
    guavaVersion = '14.0.1'
    junitVersion = '4.10'
    hamcrestVersion = '1.3'
    seleniumWebdriverVersion = '2.32.0'
    mokitoVersion = '1.9.5'
    jettyVersion = '9.0.0.v20130308'
    freemarkerVersion = '2.3.19'
  }
}

project(":simple-orm") {

  dependencies {
    compile(
        "com.google.guava:guava:$guavaVersion",
        'cglib:cglib-nodep:2.2'
    )

    testCompile(
        "junit:junit-dep:$junitVersion",
        "org.hamcrest:hamcrest-core:$hamcrestVersion",
        "org.hamcrest:hamcrest-library:$hamcrestVersion",
        "org.mockito:mockito-all:$mokitoVersion",
        'mysql:mysql-connector-java:5.1.24'
    )
  }

  task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
  }

  artifacts {
    archives jar
    archives sourcesJar
  }

  version = '0.0.1-SNAPSHOT'
  group = 'com.github.nicholasren'
}

project(":sample") {
  apply plugin: 'java'
  dependencies {
    compile project(':simple-orm')
    compile "com.github.nicholasren:simple-di:0.0.6-SNAPSHOT"
    compile "com.github.nicholasren:simple-mvc:0.0.2-SNAPSHOT"
    compile "com.github.nicholasren:simple-orm:0.0.1-SNAPSHOT"
    testCompile(
        "junit:junit-dep:$junitVersion",
        "org.hamcrest:hamcrest-core:$hamcrestVersion",
        "org.hamcrest:hamcrest-library:$hamcrestVersion",
        "org.seleniumhq.selenium:selenium-java:$seleniumWebdriverVersion",
        "org.mockito:mockito-all:$mokitoVersion",
        "org.eclipse.jetty:jetty-server:$jettyVersion",
        "org.eclipse.jetty:jetty-servlet:$jettyVersion",
        "org.eclipse.jetty:jetty-webapp:$jettyVersion",
        "org.freemarker:freemarker:$freemarkerVersion",
    )
  }

  task start(type: JavaExec, dependsOn: [classes]) {
    main = 'com.house.server.AppServer'
    classpath = sourceSets.main.runtimeClasspath
  }

}

